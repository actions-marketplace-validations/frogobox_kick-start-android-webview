apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0

    def appDomain = "com"
    def appDevConsole = "frogobox"
    def appName = "basewebview"

    def projectApplicationId = "$appDomain.$appDevConsole.$appName"
    def projectVersionCode = (versionMajor * 100) + (versionMinor * 10) + (versionPatch * 1)
    def projectVersionName = "$versionMajor.$versionMinor.$versionPatch"

    def projectStoreFile = "frogoboxdev.jks"
    def projectStorePassword = "cronoclez"
    def projectKeyAlias = "frogobox"
    def projectKeyPassword = "xeonranger"

    // Declaration app name
    def nameApp = "Base Webview"

    // Declaration admob id for release
    def releaseAdmobPublisherId = ""
    def releaseAdmobBanner = ""
    def releaseAdmobInterstitial = ""
    def releaseAdmobInterstitialVideo = ""
    def releaseAdmobRewardedVideo = ""
    def releaseAdmobNativeAdvanced = ""
    def releaseAdmobNativeAdvancedVideo = ""

    // ---------------------------------------------------------------------------------------------

    // Declaration apk / aab name
    def nameAppLower = nameApp.toLowerCase()
    def nameAPK = nameAppLower.replace(" ", "-")

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId projectApplicationId
        minSdkVersion 21
        targetSdkVersion 30
        versionCode projectVersionCode
        versionName projectVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // Declaration apps name debug mode
        def debugAttribute = " Development"
        def nameAppDebug = nameApp + debugAttribute

        // Declaration admob id for debug
        def debugAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
        def debugAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
        def debugAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"
        def debugAdmobInterstitialVideo = "ca-app-pub-3940256099942544/8691691433"
        def debugAdmobRewardedVideo = "ca-app-pub-3940256099942544/5224354917"
        def debugAdmobNativeAdvanced = "ca-app-pub-3940256099942544/2247696110"
        def debugAdmobNativeAdvancedVideo = "ca-app-pub-3940256099942544/1044960115"

        // Naming APK // AAB
        setProperty("archivesBaseName", "$nameAPK$versionName#$versionCode-${new Date().format('ddMMyy')}")

        resConfigs "en", "id"

        // Inject app name for debug
        resValue "string", "app_name", nameAppDebug

        // Inject admob id for debug
        resValue "string", "admob_publisher_id", debugAdmobPublisherId
        resValue "string", "admob_banner", debugAdmobBanner
        resValue "string", "admob_interstitial", debugAdmobInterstitial
        resValue "string", "admob_interstitial_video", debugAdmobInterstitialVideo
        resValue "string", "admob_rewarded_video", debugAdmobRewardedVideo
        resValue "string", "admob_native_advanced", debugAdmobNativeAdvanced
        resValue "string", "admob_native_advanced_video", debugAdmobNativeAdvancedVideo

    }

    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            // [PROJECT FOLDER NAME/app/[COPY YOUT KEY STORE] .jks in here
            storeFile file(projectStoreFile)
            storePassword projectStorePassword
            keyAlias projectKeyAlias
            keyPassword projectKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Generated Signed APK / AAB
            signingConfig signingConfigs.release

            // Inject app name for release
            resValue "string", "app_name", nameApp

            // Inject admob id for release
            resValue "string", "admob_publisher_id", releaseAdmobPublisherId
            resValue "string", "admob_banner", releaseAdmobBanner
            resValue "string", "admob_interstitial", releaseAdmobInterstitial
            resValue "string", "admob_interstitial_video", releaseAdmobInterstitialVideo
            resValue "string", "admob_rewarded_video", releaseAdmobRewardedVideo
            resValue "string", "admob_native_advanced", releaseAdmobNativeAdvanced
            resValue "string", "admob_native_advanced_video", releaseAdmobNativeAdvancedVideo

        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.material:material:1.4.0-beta01'
    implementation 'com.google.android.gms:play-services-ads:20.1.0'

    implementation 'com.github.amirisback:frogo-admob-helper:2.0.1'
    implementation 'com.github.amirisback:frogo-log:2.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
}
